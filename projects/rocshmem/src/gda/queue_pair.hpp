/******************************************************************************
 * Copyright (c) Advanced Micro Devices, Inc. All rights reserved.
 *
 * SPDX-License-Identifier: MIT
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to
 * deal in the Software without restriction, including without limitation the
 * rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
 * sell copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
 * FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
 * IN THE SOFTWARE.
 *****************************************************************************/

#ifndef LIBRARY_SRC_GDA_QUEUE_PAIR_HPP_
#define LIBRARY_SRC_GDA_QUEUE_PAIR_HPP_

/**
 * @file queue_pair.hpp
 *
 * @section DESCRIPTION
 * An IB QueuePair (SQ and CQ) that the device can use to perform network
 * operations. Most important rocSHMEM operations are performed by this
 * class.
 */

#include <limits>
#include <map>
#include <tuple>
#include <type_traits>

#include <hip/hip_runtime.h>

#include "rocshmem/rocshmem_config.h"  // NOLINT(build/include_subdir)
#include "rocshmem/rocshmem.hpp"
#include "endian.hpp"
#include "constants.hpp"
#include "util.hpp"

#include "ibv_wrapper.hpp"

#include "containers/free_list_impl.hpp"
#include "memory/hip_allocator.hpp"

namespace rocshmem {

enum class GDAProvider {
  UNSET,
  IONIC,
  BNXT,
  MLX5
};

struct BufferInfo {
  uintptr_t addr;
  size_t    length;
  uint32_t  lkey;
};

/**
 * @brief Scope at which WQEs are issued and completed. This is used to
 * determine how to synchronize threads and when to poll the CQ for
 * completions.
 * thread: Each thread issues WQEs independently
 * wave: Thread 0 in each wave issues WQE
 * wg: Thread 0 of WAVE 0 issues WQE
 */

enum class ThreadScope: int {
  thread,
  wave,
  wg
};

class ActiveWFInfo {
 public:
  uint64_t    activemask{0};                  // Mask of active threads in the wavefront
  uint64_t    pe_group_mask{0};               // Mask of active threads with the same PE
  int         pe{-1};                         // PE for the threads in pe_group_mask
  int         num_pe_group_lanes{0};          // Number of active lanes in pe_group_mask
  int         pe_group_logical_lane_id{0};    // Logical lane id of this thread in pe_group_mask
  int         pe_group_first_phys_lane_id{0}; // Physical lane id of first thread in pe_group_mask
  int         pe_group_last_phys_lane_id{0};  // Physical lane id of last thread in pe_group_mask
  ThreadScope scope{ThreadScope::thread};     // Threading scope
  bool        is_pe_group_first{false};       // True if this is the first thread in pe_group_mask
  bool        is_pe_group_last{false};        // True if this is the last thread in pe_group_mask

  __device__ explicit ActiveWFInfo(int pe, ThreadScope scope = ThreadScope::thread)
      : pe(pe), scope(scope) {
    // Get active lane mask
    activemask = get_active_lane_mask();

    // Get mask of active lanes with the same PE
    switch (scope) {
      case ThreadScope::thread: {
        pe_group_mask       = __match_any_sync(activemask, pe);
        num_pe_group_lanes  = get_active_lane_count(pe_group_mask);
        pe_group_logical_lane_id = get_active_lane_num(pe_group_mask);
        pe_group_first_phys_lane_id = get_first_active_lane_id(pe_group_mask);
        pe_group_last_phys_lane_id  = get_last_active_lane_id(pe_group_mask);
        break;
      }
      // Only thread 0 issues the WQE, so the group is just that thread
      case ThreadScope::wave:
      case ThreadScope::wg: {
        pe_group_mask       = 1;
        num_pe_group_lanes  = 1;
        pe_group_logical_lane_id = get_active_lane_num(activemask);
        pe_group_first_phys_lane_id = 0;
        pe_group_last_phys_lane_id  = 0;
      }
    }
    is_pe_group_first = (pe_group_logical_lane_id == 0);
    is_pe_group_last  = (pe_group_logical_lane_id == num_pe_group_lanes - 1);
  }

  // used in CAS based atomic operations at thread scope
  __device__ void update(int _pe, ThreadScope _scope = ThreadScope::thread) {
    // Get active lane mask
    activemask          = get_active_lane_mask();
    pe_group_mask       = __match_any_sync(activemask, pe);
    num_pe_group_lanes  = get_active_lane_count(pe_group_mask);
    pe_group_logical_lane_id = get_active_lane_num(pe_group_mask);
    pe_group_first_phys_lane_id = get_first_active_lane_id(pe_group_mask);
    pe_group_last_phys_lane_id  = get_last_active_lane_id(pe_group_mask);
    is_pe_group_first   = (pe_group_logical_lane_id == 0);
    is_pe_group_last    = (pe_group_logical_lane_id == num_pe_group_lanes - 1);
    scope               = _scope;
    pe                  = _pe;
  }

  __device__ void printInfo() {
    printf("PE: %d, Scope: %d, activemask: %llx, "
           "pe_group_mask: %llx, num_pe_group_lanes: %d, "
           "thread_id: %u, pe_group_logical_lane_id: %d, "
           "is_pe_group_first: %d, pe_group_first_phys_lane_id: %d, "
           "is_pe_group_last: %d, pe_group_last_phys_lane_id: %d\n",
           pe, static_cast<int>(scope), static_cast<unsigned long long>(activemask),
           static_cast<unsigned long long>(pe_group_mask), num_pe_group_lanes,
           threadIdx.x, pe_group_logical_lane_id,
           static_cast<int>(is_pe_group_first), pe_group_first_phys_lane_id,
           static_cast<int>(is_pe_group_last), pe_group_last_phys_lane_id);
  }
};

/*
 * @struct QueuePairTraits<Provider>
 * @brief Defines Provider-specific types and constants.
 *
 * Each Provider subclass of QueuePairBase<Provider> should also define a specialization
 * for QueuePairTraits<Provider> that defines the documented members:
 *   - QueuePairTraits<Provider>::OpCode
 *   - QueuePairTraits<Provider>::Endianness
 *
 * Sample specialization code for a QueuePairProvider subclass
 * of QueuePairBase<QueuePairProvider>:
 * @code
 * class QueuePairProvider;
 * template <> struct QueuePairTraits<QueuePairProvider> {
 *   enum class OpCode : uint8_t {
 *     RDMA_WRITE = ...,
 *     RDMA_READ  = ...,
 *     ATOMIC_CS  = ...,
 *     ATOMIC_FA  = ...,
 *   };
 *
 *   static constexpr endian::Order Endianness = endian::Order::...;
 * };
 * @endcode
 *
 * @tparam Provider Name of Provider class.
 */

/*
 * @enum QueuePairTraits<Provider>::OpCode
 * @brief Enumeration of the Provider-specific opcodes.
 *
 * @var QueuePairTraits<Provider>::OpCode::RDMA_WRITE
 * Provider-specific RDMA Write opcode.
 *
 * @var QueuePairTraits<Provider>::OpCode::RDMA_READ
 * Provider-specific RDMA Read opcode.
 *
 * @var QueuePairTraits<Provider>::OpCode::ATOMIC_CS
 * Provider-specific Compare-and-Swap opcode.
 *
 * @var QueuePairTraits<Provider>::OpCode::ATOMIC_FA
 * Provider-specific Fetch-Add opcode.
 */

/*
 * @var endian::Order QueuePairTraits<Provider>::Endianness
 * @brief Endianness order of data stored by the hardware for Provider.
 *
 * @qualifier static
 * @qualifier constexpr
 */

template <typename Provider>
struct QueuePairTraits;

/*
 * @brief Atomic Memory Operation fetching behavior
 */
enum class AMOFetchType {
  Blocking,
  NonBlocking,
  NonFetching,
};



/*
 * @brief CRTP mixin class supplying a common SHMEM-like interface to Queue Pair implementations.
 */
template <typename Provider>
class QueuePairSHMEM {
/**
 * @name Provider-Defined Members
 *
 * Members that are defined based on specializations of QueuePairTraits<Provider>.
 * Each Provider must supply their own definitions.
 *
 * @{
 */
public:
  /**
   * @brief Type alias for QueuePairTraits<Provider>.
   */
  using Traits = QueuePairTraits<Provider>;

  /**
   * @brief Enumeration of the opcodes for Write, Read, Fetch-Add, and Compare-and-Swap.
   *
   * A definition must be provided by the QueuePairTraits<Provider> specialization
   * of each Provider subclass.
   */
  using OpCode = typename Traits::OpCode;
/**@}*/



/**
 * @name Member Types
 *
 * @{
 */
public:
  /**
   * @brief Helper alias for defining post_wqe_amo and post_wqe_amo_single in subclasses
   */
  template <AMOFetchType Fetch>
  using amo_ret_t = std::conditional_t<Fetch == AMOFetchType::Blocking, uint64_t, void>;
/**@}*/



/**
 * @name Remote Memory Access (RMA)
 *
 * @{
 */
public:
  /**
   * @brief Create and enqueue a non-blocking put work queue entry (wqe).
   *
   * @param[in] dest Destination address for data transmission.
   * @param[in] source Source address for data transmission.
   * @param[in] nelems Size in bytes of data transmission.
   * @param[in] wf_info Wavefront information.
   *
   * @tparam RingDB Whether to ring the doorbell
   */
  template <bool RingDB = true>
  __device__ void put_nbi(void *dest, const void *source, size_t nelems,
                          const ActiveWFInfo& wf_info);
  template <bool RingDB = true>
  __device__ void put_nbi_single(void *dest, const void *source, size_t nelems);

  /**
   * @brief Create and enqueue a non-blocking get work queue entry (wqe).
   *
   * @param[in] dest Destination address for data transmission.
   * @param[in] source Source address for data transmission.
   * @param[in] nelems Size in bytes of data transmission.
   * @param[in] wf_info Wavefront information.
   */
  template <bool RingDB = true>
  __device__ void get_nbi(void *dest, const void *source, size_t nelems,
                          const ActiveWFInfo& wf_info);
  template <bool RingDB = true>
  __device__ void get_nbi_single(void *dest, const void *source, size_t nelems);
/**@}*/



/**
 * @name Atomic Memory Operations (AMO)
 *
 * @{
 */
public:
  /**
   * @brief Create and enqueue a blocking atomic fetch-and-add work queue entry (wqe).
   *
   * @param[in] dest Destination address for data transmission.
   * @param[in] value Data value for the atomic operation.
   * @param[in] wf_info Wavefront information.
   *
   * @tparam RingDB Whether to ring the doorbell
   *
   * @return An atomic value
   */
  template <bool RingDB = true>
  __device__ uint64_t atomic_fetch_add(void *dest, uint64_t value, const ActiveWFInfo& wf_info);
  template <bool RingDB = true>
  __device__ uint64_t atomic_fetch_add_single(void *dest, uint64_t value);

#if 0
  /**
   * @brief Create and enqueue a non-blocking atomic fetch-and-add work queue entry (wqe).
   *
   * @param[in] fetch Address for fetched value.
   * @param[in] dest Destination address for data transmission.
   * @param[in] value Data value for the atomic operation.
   * @param[in] wf_info Wavefront information.
   *
   * @tparam RingDB Whether to ring the doorbell
   *
   * @return An atomic value
   */
  template <bool RingDB = true>
  __device__ void atomic_fetch_add_nbi(uint64_t *fetch, void *dest, uint64_t value,
                                       const ActiveWFInfo& wf_info);
  template <bool RingDB = true>
  __device__ void atomic_fetch_add_nbi_single(uint64_t *fetch, void *dest, uint64_t value);
#endif

  /**
   * @brief Create and enqueue a non-blocking atomic add work queue entry (wqe).
   *
   * @param[in] dest Destination address for data transmission.
   * @param[in] value Data value for the atomic operation.
   * @param[in] wf_info Wavefront information.
   *
   * @tparam RingDB Whether to ring the doorbell
   */
  template <bool RingDB = true>
  __device__ void atomic_add_nbi(void *dest, uint64_t value, const ActiveWFInfo& wf_info);
  template <bool RingDB = true>
  __device__ void atomic_add_nbi_single(void *dest, uint64_t value);

  /**
   * @brief Create and enqueue a blocking atomic compare-and-swap work queue entry (wqe).
   *
   * @param[in] dest Destination address for data transmission.
   * @param[in] cond Used in atomic comparisons.
   * @param[in] value Data value for the atomic operation.
   * @param[in] wf_info Wavefront information.
   *
   * @tparam RingDB Whether to ring the doorbell
   *
   * @return An atomic value
   */
  template <bool RingDB = true>
  __device__ uint64_t atomic_cas(void *dest, uint64_t cond, uint64_t value,
                                 const ActiveWFInfo& wf_info);
  template <bool RingDB = true>
  __device__ uint64_t atomic_cas_single(void *dest, uint64_t cond, uint64_t value);

#if 0
  /**
   * @brief Create and enqueue a non-blocking atomic compare-and-swap work queue entry (wqe).
   *
   * @param[in] fetch Address for fetched value.
   * @param[in] dest Destination address for data transmission.
   * @param[in] cond Used in atomic comparisons.
   * @param[in] value Data value for the atomic operation.
   * @param[in] wf_info Wavefront information.
   *
   * @tparam RingDB Whether to ring the doorbell
   *
   * @return An atomic value
   */
  template <bool RingDB = true>
  __device__ void atomic_cas_nbi(uint64_t *fetch, void *dest, uint64_t cond, uint64_t value,
                                 const ActiveWFInfo& wf_info);
  template <bool RingDB = true>
  __device__ void atomic_cas_nbi_single(uint64_t *fetch, void *dest, uint64_t cond, uint64_t value);
#endif

  /**
   * @brief Create and enqueue a non-fetching atomic compare-and-swap work queue entry (wqe).
   *
   * @param[in] dest Destination address for data transmission.
   * @param[in] cond Used in atomic comparisons.
   * @param[in] value Data value for the atomic operation.
   * @param[in] wf_info Wavefront information.
   *
   * @tparam RingDB Whether to ring the doorbell
   *
   * @return An atomic value
   */
  template <bool RingDB = true>
  __device__ void atomic_cas_nbi_nofetch(void *dest, uint64_t cond, uint64_t value,
                                         const ActiveWFInfo& wf_info);
  template <bool RingDB = true>
  __device__ void atomic_cas_nbi_nofetch_single(void *dest, uint64_t cond, uint64_t value);
/**@}*/


/**
 * @name Completion and Ordering.
 *
 * @{
 */
public:
  /**
   * @brief Empty all completions from the completion queue.
   *
   * @param[in] wf_info Wavefront information.
   */
  __device__ void quiet(const ActiveWFInfo& wf_info);
/**@}*/



/**
 * @name Internal
 *
 * @{
 */
protected:

  /*
   * @brief Provider is a friend of QueuePairSHMEM<Provider>.
   */
  friend Provider;

  __host__  QueuePairSHMEM() = default;
  __device__ QueuePairSHMEM() = delete;

private:
  __device__ Provider& provider() {
    return static_cast<Provider&>(*this);
  }
/**@}*/
};



template <typename Provider>
template <bool RingDB>
__device__ void QueuePairSHMEM<Provider>::put_nbi(
    void *dest, const void *source, size_t nelems, const ActiveWFInfo& wf_info) {
  constexpr OpCode Op = OpCode::RDMA_WRITE;
  uintptr_t laddr = reinterpret_cast<uintptr_t>(source);
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  provider().template post_wqe_rma<Op, RingDB>(laddr, raddr, nelems, wf_info);
}

template <typename Provider>
template <bool RingDB>
__device__ void QueuePairSHMEM<Provider>::put_nbi_single(
    void *dest, const void *source, size_t nelems) {
  constexpr OpCode Op = OpCode::RDMA_WRITE;
  uintptr_t laddr = reinterpret_cast<uintptr_t>(source);
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  provider().template post_wqe_rma_single<Op, RingDB>(laddr, raddr, nelems);
}

template <typename Provider>
template <bool RingDB>
__device__ void QueuePairSHMEM<Provider>::get_nbi(
    void *dest, const void *source, size_t nelems, const ActiveWFInfo& wf_info) {
  constexpr OpCode Op = OpCode::RDMA_READ;
  uintptr_t laddr = reinterpret_cast<uintptr_t>(dest);
  uintptr_t raddr = reinterpret_cast<uintptr_t>(source);
  provider().template post_wqe_rma<Op, RingDB>(laddr, raddr, nelems, wf_info);
}

template <typename Provider>
template <bool RingDB>
__device__ void QueuePairSHMEM<Provider>::get_nbi_single(
    void *dest, const void *source, size_t nelems) {
  constexpr OpCode Op = OpCode::RDMA_READ;
  uintptr_t laddr = reinterpret_cast<uintptr_t>(dest);
  uintptr_t raddr = reinterpret_cast<uintptr_t>(source);
  provider().template post_wqe_rma_single<Op, RingDB>(laddr, raddr, nelems);
}

#if 0
template <typename Provider>
template <bool RingDB>
__device__ uint64_t QueuePairSHMEM<Provider>::atomic_fetch_add(
    void *dest, uint64_t value, const ActiveWFInfo& wf_info) {
  constexpr OpCode Op = OpCode::ATOMIC_FA;
  constexpr AMOFetchType Fetch = AMOFetchType::Blocking;
  uintptr_t laddr = /* TODO */
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  provider().template post_wqe_amo<Op, Fetch, RingDB>(laddr, raddr, value, 0, wf_info);
  quiet(wf_info);
  return *reinterpret_cast<uint64_t*>(laddr);
}

template <typename Provider>
template <bool RingDB>
__device__ uint64_t QueuePairSHMEM<Provider>::atomic_fetch_add_single(
    void *dest, uint64_t value) {
  constexpr OpCode Op = OpCode::ATOMIC_FA;
  constexpr AMOFetchType Fetch = AMOFetchType::Blocking;
  uintptr_t laddr = /* TODO */
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  provider().template post_wqe_amo_single<Op, Fetch, RingDB>(laddr, raddr, value, 0);
  provider().quiet_single();
  return *reinterpret_cast<uint64_t*>(laddr);
}

template <typename Provider>
template <bool RingDB>
__device__ void QueuePairSHMEM<Provider>::atomic_fetch_add_nbi(
    uint64_t *fetch, void *dest, uint64_t value, const ActiveWFInfo& wf_info) {
  constexpr OpCode Op = OpCode::ATOMIC_FA;
  constexpr AMOFetchType Fetch = AMOFetchType::NonBlocking;
  uintptr_t laddr = reinterpret_cast<uintptr_t>(fetch);
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  provider().template post_wqe_amo<Op, Fetch, RingDB>(laddr, raddr, value, 0, wf_info);
}

template <typename Provider>
template <bool RingDB>
__device__ void QueuePairSHMEM<Provider>::atomic_fetch_add_nbi_single(
    uint64_t *fetch, void *dest, uint64_t value) {
  constexpr OpCode Op = OpCode::ATOMIC_FA;
  constexpr AMOFetchType Fetch = AMOFetchType::NonBlocking;
  uintptr_t laddr = reinterpret_cast<uintptr_t>(fetch);
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  provider().template post_wqe_amo_single<Op, Fetch, RingDB>(laddr, raddr, value, 0);
}

template <typename Provider>
template <bool RingDB>
__device__ void QueuePairSHMEM<Provider>::atomic_add_nbi(
    void *dest, uint64_t value, const ActiveWFInfo& wf_info) {
  constexpr OpCode Op = OpCode::ATOMIC_FA;
  constexpr AMOFetchType Fetch = AMOFetchType::NonFetching;
  uintptr_t laddr = reinterpret_cast<uintptr_t>(nonfetching_atomic);
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  provider().template post_wqe_amo<Op, Fetch, RingDB>(laddr, raddr, value, 0, wf_info);
}

template <typename Provider>
template <bool RingDB>
__device__ void QueuePairSHMEM<Provider>::atomic_add_nbi_single(void *dest, uint64_t value) {
  constexpr OpCode Op = OpCode::ATOMIC_FA;
  constexpr AMOFetchType Fetch = AMOFetchType::NonFetching;
  uintptr_t laddr = reinterpret_cast<uintptr_t>(nonfetching_atomic);
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  provider().template post_wqe_amo_single<Op, Fetch, RingDB>(laddr, raddr, value, 0);
}

template <typename Provider>
template <bool RingDB>
__device__ uint64_t QueuePairSHMEM<Provider>::atomic_cas(
    void *dest, uint64_t cond, uint64_t value, const ActiveWFInfo& wf_info) {
  constexpr OpCode Op = OpCode::ATOMIC_CS;
  constexpr AMOFetchType Fetch = AMOFetchType::Blocking;
  uintptr_t laddr = /* TODO */
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  provider().template post_wqe_amo<Op, Fetch, RingDB>(laddr, raddr, value, cond, wf_info);
  quiet(wf_info);
  return *reinterpret_cast<uint64_t*>(laddr);
}

template <typename Provider>
template <bool RingDB>
__device__ uint64_t QueuePairSHMEM<Provider>::atomic_cas_single(
    void *dest, uint64_t cond, uint64_t value) {
  constexpr OpCode Op = OpCode::ATOMIC_CS;
  constexpr AMOFetchType Fetch = AMOFetchType::Blocking;
  uintptr_t laddr = /* TODO */
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  provider().template post_wqe_amo_single<Op, Fetch, RingDB>(laddr, raddr, value, cond);
  quiet_single();
  return *reinterpret_cast<uint64_t*>(laddr);
}

template <typename Provider>
template <bool RingDB>
__device__ void QueuePairSHMEM<Provider>::atomic_cas_nbi(
    uint64_t *fetch, void *dest, uint64_t cond, uint64_t value, const ActiveWFInfo& wf_info) {
  constexpr OpCode Op = OpCode::ATOMIC_CS;
  constexpr AMOFetchType Fetch = AMOFetchType::NonBlocking;
  uintptr_t laddr = reinterpret_cast<uintptr_t>(fetch);
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  provider().template post_wqe_amo<Op, Fetch, RingDB>(laddr, raddr, value, cond, wf_info);
}

template <typename Provider>
template <bool RingDB>
__device__ void QueuePairSHMEM<Provider>::atomic_cas_nbi_single(
    uint64_t *fetch, void *dest, uint64_t cond, uint64_t value) {
  constexpr OpCode Op = OpCode::ATOMIC_CS;
  constexpr AMOFetchType Fetch = AMOFetchType::NonBlocking;
  uintptr_t laddr = reinterpret_cast<uintptr_t>(fetch);
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  provider().template post_wqe_amo_single<Op, Fetch, RingDB>(laddr, raddr, value, cond);
}

template <typename Provider>
template <bool RingDB>
__device__ void QueuePairSHMEM<Provider>::atomic_cas_nbi_nofetch(
    void *dest, uint64_t cond, uint64_t value, const ActiveWFInfo& wf_info) {
  constexpr OpCode Op = OpCode::ATOMIC_CS;
  constexpr AMOFetchType Fetch = AMOFetchType::NonFetching;
  uintptr_t laddr = reinterpret_cast<uintptr_t>(nonfetching_atomic);
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  provider().template post_wqe_amo<Op, Fetch, RingDB>(laddr, raddr, value, cond, wf_info);
}

template <typename Provider>
template <bool RingDB>
__device__ void QueuePairSHMEM<Provider>::atomic_cas_nbi_nofetch_single(
    void *dest, uint64_t cond, uint64_t value) {
  constexpr OpCode Op = OpCode::ATOMIC_CS;
  constexpr AMOFetchType Fetch = AMOFetchType::NonFetching;
  uintptr_t laddr = reinterpret_cast<uintptr_t>(nonfetching_atomic);
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  provider().template post_wqe_amo_single<Op, Fetch, RingDB>(laddr, raddr, value, cond);
}
#endif

template <typename Provider>
template <bool RingDB>
__device__ uint64_t QueuePairSHMEM<Provider>::atomic_fetch_add(
    void *dest, uint64_t value, const ActiveWFInfo& wf_info) {
  constexpr OpCode Op = OpCode::ATOMIC_FA;
  constexpr AMOFetchType Fetch = AMOFetchType::Blocking;
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  return provider().template post_wqe_amo<Op, Fetch, RingDB>(raddr, value, 0, wf_info);
}

template <typename Provider>
template <bool RingDB>
__device__ uint64_t QueuePairSHMEM<Provider>::atomic_fetch_add_single(
    void *dest, uint64_t value) {
  constexpr OpCode Op = OpCode::ATOMIC_FA;
  constexpr AMOFetchType Fetch = AMOFetchType::Blocking;
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  return provider().template post_wqe_amo_single<Op, Fetch, RingDB>(raddr, value, 0);
}

template <typename Provider>
template <bool RingDB>
__device__ void QueuePairSHMEM<Provider>::atomic_add_nbi(
    void *dest, uint64_t value, const ActiveWFInfo& wf_info) {
  constexpr OpCode Op = OpCode::ATOMIC_FA;
  constexpr AMOFetchType Fetch = AMOFetchType::NonFetching;
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  provider().template post_wqe_amo<Op, Fetch, RingDB>(raddr, value, 0, wf_info);
}

template <typename Provider>
template <bool RingDB>
__device__ void QueuePairSHMEM<Provider>::atomic_add_nbi_single(void *dest, uint64_t value) {
  constexpr OpCode Op = OpCode::ATOMIC_FA;
  constexpr AMOFetchType Fetch = AMOFetchType::NonFetching;
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  provider().template post_wqe_amo_single<Op, Fetch, RingDB>(raddr, value, 0);
}

template <typename Provider>
template <bool RingDB>
__device__ uint64_t QueuePairSHMEM<Provider>::atomic_cas(
    void *dest, uint64_t cond, uint64_t value, const ActiveWFInfo& wf_info) {
  constexpr OpCode Op = OpCode::ATOMIC_CS;
  constexpr AMOFetchType Fetch = AMOFetchType::Blocking;
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  return provider().template post_wqe_amo<Op, Fetch, RingDB>(raddr, value, cond, wf_info);
}

template <typename Provider>
template <bool RingDB>
__device__ uint64_t QueuePairSHMEM<Provider>::atomic_cas_single(
    void *dest, uint64_t cond, uint64_t value) {
  constexpr OpCode Op = OpCode::ATOMIC_CS;
  constexpr AMOFetchType Fetch = AMOFetchType::Blocking;
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  return provider().template post_wqe_amo_single<Op, Fetch, RingDB>(raddr, value, cond);
}

template <typename Provider>
template <bool RingDB>
__device__ void QueuePairSHMEM<Provider>::atomic_cas_nbi_nofetch(
    void *dest, uint64_t cond, uint64_t value, const ActiveWFInfo& wf_info) {
  constexpr OpCode Op = OpCode::ATOMIC_CS;
  constexpr AMOFetchType Fetch = AMOFetchType::NonFetching;
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  provider().template post_wqe_amo<Op, Fetch, RingDB>(raddr, value, cond, wf_info);
}

template <typename Provider>
template <bool RingDB>
__device__ void QueuePairSHMEM<Provider>::atomic_cas_nbi_nofetch_single(
    void *dest, uint64_t cond, uint64_t value) {
  constexpr OpCode Op = OpCode::ATOMIC_CS;
  constexpr AMOFetchType Fetch = AMOFetchType::NonFetching;
  uintptr_t raddr = reinterpret_cast<uintptr_t>(dest);
  provider().template post_wqe_amo_single<Op, Fetch, RingDB>(raddr, value, cond);
}

template <typename Provider>
__device__ void QueuePairSHMEM<Provider>::quiet(const ActiveWFInfo& wf_info) {
  if (wf_info.is_pe_group_first) {
    provider().quiet_single();
  }
}



/*
 * @brief CRTP base class for Provider-specific Queue Pair implementations.
 */
template <typename Provider>
class QueuePairBase : public QueuePairSHMEM<Provider> {
/**
 * @name Provider-Defined Members
 *
 * Members that are defined based on specializations of QueuePairTraits<Provider>.
 * Each Provider must supply their own definitions.
 *
 * @{
 */
public:
  /**
   * @brief Type alias for QueuePairTraits<Provider>.
   */
  using Traits = QueuePairTraits<Provider>;

  /**
   * @brief Constant defining the endianness required by the provider. Used for e.g. lkey and rkey.
   *
   * A definition must be provided by the QueuePairTraits<Provider> specialization
   * of each Provider subclass.
   */
  static constexpr endian::Order ProviderEndianness = Traits::Endianness;
/**@}*/



/**
 * @name Constructors and Destructors
 *
 * @{
 */
public:
  /**
   * @brief Constructor.
   *
   * @param[in] qpn Queue Pair number.
   * @param[in] base_heap Base address of local heap.
   * @param[in] heap_size Size of heap, in bytes.
   * @param[in] lkey LKey of local heap.
   * @param[in] rkey RKey of remote heap.
   * @param[in] pd IBVerbs Protection Domain for registering additional buffers and heaps.
   */
  __host__ explicit QueuePairBase(uint32_t qpn, void *base_heap, size_t heap_size,
                                  uint32_t lkey, uint32_t rkey, struct ibv_pd* pd);

protected:
  /**
   * @brief Copy and Move Constructors and Assignment Operators.
   */
  /* NOTE: do we need to create definitions for these? define as deleted? */
  /* move-only type? */

  /*
   * @brief Copy constructor is deleted.
   */
  __host__ QueuePairBase(const QueuePairBase& other) = delete;

  /*
   * @brief Copy assignment operator is deleted.
   */
  __host__ QueuePairBase& operator=(const QueuePairBase& other) = delete;

  /*
   * @brief Move constructor.
   *
   * Performs a member-wise move of all data members from other to *this,
   * then resets allocated data members of other so that it can be safely reused or destroyed.
   *
   * @param[in,out] other QueuePairBase object to move from.
   */
  __host__ QueuePairBase(QueuePairBase&& other) noexcept;

  /*
   * @brief Move assignment operator.
   *
   * Cleans up all resources allocated by *this,
   * performs a member-wise move of all data members from other to *this,
   * then resets allocated data members of other so that it can be safely reused or destroyed.
   *
   * @param[in,out] other QueuePairBase object to move from.
   * @return *this
   */
  __host__ QueuePairBase& operator=(QueuePairBase&& other);

  /**
   * @brief Destructor.
   *
   * Cleans up all resources allocated by *this.
   */
  __host__   ~QueuePairBase();
/**@}*/



/**
 * @name Buffer Registration
 *
 * @{
 */
public:
  /**
   * @brief Register buffer for use as local address in rocSHMEM routines
   *
   * @param[in] addr Base address of buffer.
   * @param[in] length Length of buffer.
   *
   * @retval ROCSHMEM_SUCCESS Buffer registered successfully.
   * @retval ROCSHMEM_ERROR Buffer could not be registered.
   */
  __host__ int buffer_register(void *addr, size_t length);

  /**
   * @brief Unregister buffer.
   * Buffer must have previously been registered with buffer_register(void *, size_t).
   *
   * @param[in] addr Base address of buffer.
   *
   * @retval ROCSHMEM_SUCCESS Buffer unregistered successfully.
   */
  __host__ int buffer_unregister(void *addr);

protected:
  /**
   * @brief Retrieve LKey for address.
   *
   * @param[in] addr Address to lookup LKey of.
   *
   * @return LKey for addr or std::numeric_limits<uint32_t>::max() if not found.
   * Endianness of returned LKey value is ProviderEndianness.
   */
  __device__ uint32_t get_lkey(uintptr_t addr);
/**@}*/



/**
 * @name Internal
 *
 * @{
 */
protected:
  /*
   * @brief Provider is a friend of QueuePairBase<Provider>.
   */
  friend Provider;

  template <AMOFetchType Fetch>
  __device__ constexpr uint64_t* get_atomic_addr();

  template <AMOFetchType Fetch>
  __device__ constexpr uint32_t get_atomic_lkey();

private:
  __device__ Provider& provider() {
    return static_cast<Provider&>(*this);
  }

  template <typename T>
  __host__ std::tuple<T*, struct ibv_mr*, uint32_t> allocate_and_register(size_t size, int access);
/**@}*/



/**
 * @name Non-static Data Members
 *
 * @{
 */
public:
  uintptr_t base_heap;
  size_t heap_size;

protected:
  // Used in most WQEs
  uint32_t qp_num;
  uint32_t lkey;
  uint32_t rkey;
  uint32_t fetching_atomic_idx{0};

private:
  // Used by get_lkey
  size_t num_user_buffers{0};
  BufferInfo* buffer_info{nullptr};

protected:
  // Used in atomic WQEs
  uint64_t* fetching_atomic{nullptr};
  uint64_t* nonfetching_atomic{nullptr};

  uint32_t fetching_atomic_lkey{0};
  uint32_t nonfetching_atomic_lkey{0};

  static constexpr size_t FETCHING_ATOMIC_CNT{1024};
  static_assert(FETCHING_ATOMIC_CNT % WF_SIZE == 0);
  using FreeListT = FreeList<uint64_t*>;
  FreeListT* fetching_atomic_freelist{nullptr};

  HIPAllocator allocator{};

private:
  // Used by host in buffer_register, buffer_unregister
  std::map<uintptr_t, struct ibv_mr*> buffer_mr_map{};
  struct ibv_pd* pd;
  struct ibv_mr* fetching_atomic_mr{nullptr};
  struct ibv_mr* nonfetching_atomic_mr{nullptr};
/**@}*/
};



template <typename Provider>
__host__ QueuePairBase<Provider>::QueuePairBase(uint32_t qpn, void *base_heap, size_t heap_size,
                                                uint32_t lkey, uint32_t rkey, struct ibv_pd* pd)
  : base_heap{reinterpret_cast<uintptr_t>(base_heap)},
    heap_size{heap_size},
    qp_num{qpn},
    lkey{endian::from_native<ProviderEndianness>(lkey)},
    rkey{endian::from_native<ProviderEndianness>(rkey)},
    pd{pd} {
  int access = IBV_ACCESS_LOCAL_WRITE
             | IBV_ACCESS_REMOTE_WRITE
             | IBV_ACCESS_REMOTE_READ
             | IBV_ACCESS_REMOTE_ATOMIC;

  if (envvar::gda::pcie_relaxed_ordering) {
    access |= IBV_ACCESS_RELAXED_ORDERING;
  }

  // Allocate and register the fetching and nonfetching atomics arrays
  std::tie(nonfetching_atomic, nonfetching_atomic_mr, nonfetching_atomic_lkey)
      = allocate_and_register<uint64_t>(1, access);
  std::tie(fetching_atomic, fetching_atomic_mr, fetching_atomic_lkey)
      = allocate_and_register<uint64_t>(FETCHING_ATOMIC_CNT, access);

  allocator.allocate(reinterpret_cast<void**>(&fetching_atomic_freelist), sizeof(FreeListT));
  new (fetching_atomic_freelist) FreeListT{allocator};

  int deviceId;
  CHECK_HIP(hipGetDevice(&deviceId));
  int wf_size = get_wf_size(deviceId);
  for (size_t i = 0; i < FETCHING_ATOMIC_CNT; i += wf_size) {
    fetching_atomic_freelist->push_back(&fetching_atomic[i]);
  }

  /* Setup User Buffer Registration Mechanism */
  num_user_buffers = envvar::gda::num_user_buffers;

  CHECK_HIP(hipMalloc(&buffer_info, sizeof(BufferInfo) * num_user_buffers));
  CHECK_HIP(hipMemset(buffer_info, 0, sizeof(BufferInfo) * num_user_buffers));
}

template <typename Provider>
__host__ QueuePairBase<Provider>::QueuePairBase(QueuePairBase&& other) noexcept
  : base_heap               {std::move(other.base_heap)},
    heap_size               {std::move(other.heap_size)},
    qp_num                  {std::move(other.qp_num)},
    lkey                    {std::move(other.lkey)},
    rkey                    {std::move(other.rkey)},
    fetching_atomic_idx     {std::move(other.fetching_atomic_idx)},
    num_user_buffers        {std::move(other.num_user_buffers)},
    buffer_info             {std::move(other.buffer_info)},
    fetching_atomic         {std::move(other.fetching_atomic)},
    nonfetching_atomic      {std::move(other.nonfetching_atomic)},
    fetching_atomic_lkey    {std::move(other.fetching_atomic_lkey)},
    nonfetching_atomic_lkey {std::move(other.nonfetching_atomic_lkey)},
    fetching_atomic_freelist{std::move(other.fetching_atomic_freelist)},
    allocator               {std::move(other.allocator)},
    buffer_mr_map           {std::move(other.buffer_mr_map)},
    pd                      {std::move(other.pd)},
    fetching_atomic_mr      {std::move(other.fetching_atomic_mr)},
    nonfetching_atomic_mr   {std::move(other.nonfetching_atomic_mr)} {
  other.buffer_info              = nullptr;
  other.fetching_atomic          = nullptr;
  other.nonfetching_atomic       = nullptr;
  other.fetching_atomic_freelist = nullptr;
  other.fetching_atomic_mr       = nullptr;
  other.nonfetching_atomic_mr    = nullptr;
}

template <typename Provider>
__host__ QueuePairBase<Provider>& QueuePairBase<Provider>::operator=(QueuePairBase&& other) {
  int err = 0;

  /* Step 1: ensure all resources in *this are deallocated */
  if (!buffer_mr_map.empty()) {
    LOG_WARN("Unmatched buffer_register detected: "
             "move assignment operator %s called, but buffer registration map is not empty!",
             __PRETTY_FUNCTION__ );
    for (auto&& [addrint, mr] : buffer_mr_map) {
      err = ibv.dereg_mr(mr);
      CHECK_ZERO(err, "ibv_dereg_mr (QueuePairBase<Provider>::operator=(QueuePairBase&&))");
    }
  }

  if (buffer_info) {
    CHECK_HIP(hipFree(buffer_info));
  }

  if (fetching_atomic_freelist) {
    fetching_atomic_freelist->~FreeListT();
    allocator.deallocate(static_cast<void*>(fetching_atomic_freelist));
  }

  if (fetching_atomic_mr) {
    err = ibv.dereg_mr(fetching_atomic_mr);
    CHECK_ZERO(err, "ibv_dereg_mr (fetching_atomic)");
  }

  if (fetching_atomic) {
    allocator.deallocate(static_cast<void*>(fetching_atomic));
  }

  if (nonfetching_atomic_mr) {
    err = ibv.dereg_mr(nonfetching_atomic_mr);
    CHECK_ZERO(err, "ibv_dereg_mr (nonfetching_atomic)");
  }

  if (nonfetching_atomic) {
    allocator.deallocate(static_cast<void*>(nonfetching_atomic));
  }

  /* Step 2: member-wise move of all data members from other to *this */
  base_heap                = std::move(other.base_heap);
  heap_size                = std::move(other.heap_size);
  qp_num                   = std::move(other.qp_num);
  lkey                     = std::move(other.lkey);
  rkey                     = std::move(other.lkey);
  fetching_atomic_idx      = std::move(other.fetching_atomic_idx);
  num_user_buffers         = std::move(other.num_user_buffers);
  buffer_info              = std::move(other.buffer_info);
  fetching_atomic          = std::move(other.fetching_atomic);
  nonfetching_atomic       = std::move(other.nonfetching_atomic);
  fetching_atomic_lkey     = std::move(other.fetching_atomic_lkey);
  nonfetching_atomic_lkey  = std::move(other.nonfetching_atomic_lkey);
  fetching_atomic_freelist = std::move(other.fetching_atomic_freelist);
  allocator                = std::move(other.allocator);
  buffer_mr_map            = std::move(other.buffer_mr_map);
  pd                       = std::move(other.pd);
  fetching_atomic_mr       = std::move(other.fetching_atomic_mr);
  nonfetching_atomic_mr    = std::move(other.nonfetching_atomic_mr);

  /* Step 3: reset allocated data members of other so that it can be safely reused or destroyed */
  other.buffer_info              = nullptr;
  other.fetching_atomic          = nullptr;
  other.nonfetching_atomic       = nullptr;
  other.fetching_atomic_freelist = nullptr;
  other.fetching_atomic_mr       = nullptr;
  other.nonfetching_atomic_mr    = nullptr;

  /* Step 4: return *this */
  return *this;
}

template <typename Provider>
__host__ QueuePairBase<Provider>::~QueuePairBase() {
  int err = 0;

  if (!buffer_mr_map.empty()) {
    LOG_WARN("Unmatched buffer_register detected: "
             "destructor %s called, but buffer registration map is not empty!",
             __PRETTY_FUNCTION__ );
    for (auto&& [addrint, mr] : buffer_mr_map) {
      err = ibv.dereg_mr(mr);
      CHECK_ZERO(err, "ibv_dereg_mr (QueuePairBase<Provider>::~QueuePairBase)");
    }
  }

  if (buffer_info) {
    CHECK_HIP(hipFree(buffer_info));
  }

  if (fetching_atomic_freelist) {
    fetching_atomic_freelist->~FreeListT();
    allocator.deallocate(static_cast<void*>(fetching_atomic_freelist));
  }

  if (fetching_atomic_mr) {
    err = ibv.dereg_mr(fetching_atomic_mr);
    CHECK_ZERO(err, "ibv_dereg_mr (fetching_atomic)");
  }

  if (fetching_atomic) {
    allocator.deallocate(static_cast<void*>(fetching_atomic));
  }

  if (nonfetching_atomic_mr) {
    err = ibv.dereg_mr(nonfetching_atomic_mr);
    CHECK_ZERO(err, "ibv_dereg_mr (nonfetching_atomic)");
  }

  if (nonfetching_atomic) {
    allocator.deallocate(static_cast<void*>(nonfetching_atomic));
  }
}

template <typename Provider>
template <typename T>
__host__ std::tuple<T*, struct ibv_mr*, uint32_t>
QueuePairBase<Provider>::allocate_and_register(size_t count, int access) {
  void* ptr = nullptr;
  size_t size = sizeof(T) * count;
  allocator.allocate(&ptr, size);
  CHECK_HIP(hipMemset(ptr, 0, size));
  struct ibv_mr* mr = ibv.reg_mr(pd, ptr, size, access, &allocator);
  CHECK_NNULL(mr, "ibv_reg_mr");
  return {static_cast<T*>(ptr), mr, endian::from_native<ProviderEndianness>(mr->lkey)};
}

template <typename Provider>
__host__ int QueuePairBase<Provider>::buffer_register(void *addr, size_t length) {
  if (buffer_mr_map.size() >= num_user_buffers) {
    LOG_WARN("Unable to register user buffer with QP. "
             "Please increase the value of %s.", envvar::gda::num_user_buffers.get_name().c_str());
    return ROCSHMEM_ERROR;
  }

  int access = IBV_ACCESS_LOCAL_WRITE
             | IBV_ACCESS_REMOTE_WRITE
             | IBV_ACCESS_REMOTE_READ
             | IBV_ACCESS_REMOTE_ATOMIC;

  if (envvar::gda::pcie_relaxed_ordering) {
    access |= IBV_ACCESS_RELAXED_ORDERING;
  }

  struct ibv_mr* mr = ibv.reg_mr(pd, addr, length, access, &allocator);
  CHECK_NNULL(mr, "ibv_reg_mr (buffer_register)");

  uintptr_t addr_int = reinterpret_cast<uintptr_t>(addr);
  buffer_mr_map[addr_int] = mr;

  for (size_t i = 0; i < num_user_buffers; i++) {
    if (buffer_info[i].addr == 0) {
      buffer_info[i].addr   = addr_int;
      buffer_info[i].length = length;
      buffer_info[i].lkey   = endian::from_native<ProviderEndianness>(mr->lkey);
      break;
    }
  }

  return ROCSHMEM_SUCCESS;
}

template <typename Provider>
__host__ int QueuePairBase<Provider>::buffer_unregister(void *addr) {
  // TODO: we don't verify that addr was actually registered, we should do that
  uintptr_t addr_int = reinterpret_cast<uintptr_t>(addr);
  for (size_t i = 0; i < num_user_buffers; i++) {
    if (is_ptr_in_range(buffer_info[i].addr, buffer_info[i].length, addr_int)) {
      CHECK_HIP(hipMemset(&buffer_info[i], 0, sizeof(BufferInfo)));
      break;
    }
  }

  int err = ibv.dereg_mr(buffer_mr_map[addr_int]);
  CHECK_ZERO(err, "ibv_dereg_mr (buffer_unregister)");

  buffer_mr_map.erase(addr_int);

  return ROCSHMEM_SUCCESS;
}

template <typename Provider>
__device__ uint32_t QueuePairBase<Provider>::get_lkey(uintptr_t addr) {
  /* Check if in heap */
  if (is_ptr_in_range(base_heap, heap_size, addr)) {
    return lkey;
  }

  /* Get the correct lkey for the user buffer */
  for (size_t i = 0; i < num_user_buffers; i++) {
    if (is_ptr_in_range(buffer_info[i].addr, buffer_info[i].length, addr)) {
      return buffer_info[i].lkey;
    }
  }

  LOGD_ERROR_ABORT("Valid lkey for address %p not found", reinterpret_cast<void*>(addr));
  return std::numeric_limits<uint32_t>::max();
}

template <typename Provider>
template <AMOFetchType Fetch>
__device__ constexpr uint64_t* QueuePairBase<Provider>::get_atomic_addr() {
  static_assert(Fetch != AMOFetchType::NonBlocking);
  if constexpr (Fetch == AMOFetchType::Blocking) {
    return fetching_atomic;
  } else if constexpr (Fetch == AMOFetchType::NonFetching) {
    return nonfetching_atomic;
  }
}

template <typename Provider>
template <AMOFetchType Fetch>
__device__ constexpr uint32_t QueuePairBase<Provider>::get_atomic_lkey() {
  static_assert(Fetch != AMOFetchType::NonBlocking);
  if constexpr (Fetch == AMOFetchType::Blocking) {
    return fetching_atomic_lkey;
  } else if constexpr (Fetch == AMOFetchType::NonFetching) {
    return nonfetching_atomic_lkey;
  }
}

}  // namespace rocshmem

#endif  // LIBRARY_SRC_GDA_QUEUE_PAIR_HPP_
